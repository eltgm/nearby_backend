buildscript {
    ext {
        apiModules = [
                "userApi",
                "adminApi"
        ]
    }
    dependencies {
        classpath 'org.hidetake.swagger.generator:org.hidetake.swagger.generator.gradle.plugin:2.18.2'
    }
}

plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'ru.vsultanyarov'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'org.hidetake.swagger.generator'

configurations {
    swaggerCodegenV2
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    //Swagger Codegen
    swaggerCodegenV2 'io.swagger:swagger-codegen-cli:2.4.9'
    implementation 'io.swagger:swagger-annotations:1.5.20'
    implementation 'io.swagger:swagger-core:1.5.20'

    //Mapping support
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

swaggerSources {
    userApi {
        inputFile = file('spec/user_api.yaml')
        code {
            language = 'spring'
            configuration = configurations.swaggerCodegenV2
            components = [models: true, apis: true]
            additionalProperties = ['modelPackage' : 'ru.vsultanyarov.nearby_backend.mods.dto',
                                    'apiPackage'   : 'ru.vsultanyarov.nearby_backend.mods',
                                    'interfaceOnly': 'true',
                                    'dateLibrary'  : 'java8']
            // Validate YAML before code generation
            dependsOn validation
        }
    }

    adminApi {
        inputFile = file('spec/admin_api.yaml')
        code {
            language = 'spring'
            configuration = configurations.swaggerCodegenV2
            components = [ apis: true]
            additionalProperties = ['modelPackage' : 'ru.vsultanyarov.nearby_backend.mods.dto',
                                    'apiPackage'   : 'ru.vsultanyarov.nearby_backend.mods',
                                    'interfaceOnly': 'true',
                                    'dateLibrary'  : 'java8']
            // Validate YAML before code generation
            dependsOn validation
        }
    }
}

sourceSets.main.java.srcDirs "$buildDir/generated/src/main/java"
sourceSets.test.java.srcDirs "$buildDir/generated/src/main/java"
sourceSets.main.java.srcDirs apiModules.collect { "${swaggerSources."$it".code.outputDir}/src/main/java" }
sourceSets.main.resources.srcDirs apiModules.collect { "${swaggerSources."$it".code.outputDir}/src/main/resources" }
sourceSets.main.output.resourcesDir = compileJava.destinationDir

processResources {
    from('spec') { into 'api-spec' }
}

compileJava {
    options.compilerArgs << '-Amapstruct.defaultComponentModel=spring'
    dependsOn processResources
    dependsOn apiModules.collect { swaggerSources."$it".code }
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    archiveFileName = 'nearby.jar'
    archiveExtension = '.jar'
}